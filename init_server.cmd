@echo off
setlocal enabledelayedexpansion

@REM check if CrispyCrumbs server already running
set port="1324"
set pid=
for /f "tokens=5" %%a in ('netstat -ano ^| findstr :%port%') do set "pid=%%a"
if not "[%pid%]"=="[]" (
    echo CrispyCrumbs server already running
    exit /b
)

echo initializing CrispyCrumbs server...

@REM Update jwtSecret
if not [%1]==[-silent] (
    set /p jwtSecret="Enter new CrispyCrumbs server JWT secret (leave blank to keep previous): "
    if not "[!jwtSecret!]"=="[]" (
        set jwtSecret=!jwtSecret:"=!
    )
    if not "[!jwtSecret!]"=="[]" (
        set jwtSecret=!jwtSecret:'=!
    )
    if not "[!jwtSecret!]"=="[]" (
        echo //WARNING. this file is generated by init_server.cmd % > .\config\config.js
        echo module.exports = { >> .\config\config.js
        echo   mongodbUri: 'mongodb://localhost:27017/CrispyCrumbs', >> .\config\config.js
        echo   jwtSecret: '!jwtSecret!', >> .\config\config.js
        echo }; >> .\config\config.js
    )
)

@REM update MongoDB
if not exist .\FilesForMongoDB\mongosh.exe (
    .\FilesForMongoDB\unzip.exe .\FilesForMongoDB\mongosh.zip -d .\FilesForMongoDB
)

@REM Clear everything from the CrispyCrumbs database
.\FilesForMongoDB\mongosh.exe CrispyCrumbs --eval "db.dropDatabase()"

@REM Load data into the CrispyCrumbs database
.\FilesForMongoDB\mongoimport.exe --db CrispyCrumbs --collection users --file .\FilesForMongoDB\CrispyCrumbs.users.json --jsonArray
.\FilesForMongoDB\mongoimport.exe --db CrispyCrumbs --collection videos --file .\FilesForMongoDB\CrispyCrumbs.videos.json --jsonArray

.\FilesForMongoDB\mongosh.exe "CrispyCrumbs" --eval "db.videos.createIndex({ title: 'text', description: 'text' })"
.\FilesForMongoDB\mongosh.exe "CrispyCrumbs" --eval "db.videos.createIndex({ title: 1 })"
.\FilesForMongoDB\mongosh.exe "CrispyCrumbs" --eval "db.videos.createIndex({ tags: 1 })"

@REM Start the server
CALL npm install
node server.js
